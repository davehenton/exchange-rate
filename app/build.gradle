apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

apply from: "${rootDir}/environment/environment.gradle"
apply from: "${rootDir}/gradle/quality.gradle"

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.exchangerate"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file("../environment/release-keystore.jks")
            storePassword getPrivateConfig("EXCHANGE_RATE_RELEASE_SIGNING_STORE_PASSWORD")
            keyAlias getPrivateConfig("EXCHANGE_RATE_RELEASE_SIGNING_KEY_ALIAS")
            keyPassword getPrivateConfig("EXCHANGE_RATE_RELEASE_SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled = true

            buildConfigField "String", "EXCHANGE_RATE_API_KEY", getApiKey()
        }
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false
            testCoverageEnabled = true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'EXCHANGE_RATE_API_KEY', getApiKey()
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet false
        abortOnError false
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // ANDROID
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "com.android.support:design:$support_version"
    kapt "com.android.databinding:compiler:$gradle_version"
    implementation "com.android.support:multidex:$multidex_version"

    // REACTIVE
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rx_kotlin_version"
    implementation "com.jakewharton.rxbinding2:rxbinding:$rx_binding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rx_binding_version"

    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // DAGGER
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_interceptor_version"

    // ARCHITECTURE COMPONENTS
    implementation "android.arch.lifecycle:extensions:$architecture_components_version"
    implementation "android.arch.lifecycle:reactivestreams:$architecture_components_version"
    kapt "android.arch.lifecycle:compiler:$architecture_components_version"
    implementation "android.arch.persistence.room:runtime:$room_database_version"
    implementation "android.arch.persistence.room:rxjava2:$room_database_version"
    kapt "android.arch.persistence.room:compiler:$room_database_version"
    implementation "android.arch.paging:runtime:$paging_version"

    // FIREBASE
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-perf:$firebase_version"
    implementation "com.firebaseui:firebase-ui-auth:$firebase_ui_auth_version"

    // TEST
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"

    // DEVELOPMENT
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

}

def getApiKey() {
    def value = ext.environment.findValueFor("EXCHANGE_RATE_APP_API_KEY")
    return "\"$value\""
}

def getPrivateConfig(String key) {
    return ext.environment.findValueFor(key)
}

apply plugin: 'com.google.gms.google-services'